apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'nu.studer.plugindev'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.3'
    }
}

ext {
    projectName = 'vcs-semantic-version'
    projectVersion = '0.1.0-beta.1'
    projectGroup = 'com.github.moleksyuk'
    projectLicense = 'MIT'
    projectURL = 'https://github.com/moleksyuk/vcs-semantic-version'
    projectDescription = 'Gradle plugin for building project semantic version based on this http://semver.org/ specification.'
    projectOwnerId = 'moleksyuk'
    projectOwner = 'Mykhailo Oleksiuk'
    projectOwnerEmail = 'mykhailo.oleksiuk@gmail.com'
}

ext.sharedManifest = manifest {
    attributes(
            'Manifest-Version': '1.0',
            'Package': projectGroup,
            'Implementation-Title': name,
            'Implementation-Version': projectVersion,
            'Implementation-Vendor': projectOwner,
            'Built-By': projectOwner,
            'Built-Date': new Date().toString(),
            'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
            'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    )
}

version = projectVersion
group = projectGroup
description = projectDescription

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

plugindev {
    pluginImplementationClass 'com.github.moleksyuk.plugin.SemanticVersionPlugin'
    pluginDescription projectDescription
    pluginLicenses projectLicense
    pluginTags 'gradle', 'plugin', 'semantic version', 'vcs', 'versioning', 'auto version'
    authorId projectOwnerId
    authorName projectOwner
    authorEmail projectOwnerEmail
    projectUrl projectURL
    projectInceptionYear '2015'
    done()
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApiKey"
    pkg.repo = 'maven'
}