plugins {
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'

    id 'com.github.kt3k.coveralls' version '2.1.0'
    id 'com.jfrog.bintray' version '1.0'
}

version = '1.0.1'
group = 'com.github.moleksyuk'
description = 'Gradle plugin for populating project.version property based on semantic version specification (http://semver.org/).'

ext {
    projectUrl = 'https://github.com/moleksyuk/vcs-semantic-version'
    projectissueTrackerUrl = 'https://github.com/moleksyuk/vcs-semantic-version/issues'
    projectLicenseUrl = 'https://github.com/moleksyuk/vcs-semantic-version/blob/master/LICENSE'

    projectVcsUrl = 'https://github.com/moleksyuk/vcs-semantic-version.git'
    projectScmUrl = 'git@github.com:moleksyuk/vcs-semantic-version.git'
    projectScmConnection = 'scm:git:git@github.com:moleksyuk/vcs-semantic-version.git'
    projectScmDeveloperConnection = 'scm:git:git@github.com:moleksyuk/vcs-semantic-version.git'

    projectOwner = 'Mykhailo Oleksiuk'
    projectOwnerId = 'moleksyuk'
    projectOwnerEmail = 'mykhailo.oleksiuk@gmail.com'

    mavenLicenseName = 'MIT License'
    bintrayLicenseName = 'MIT'
}

ext.sharedManifest = manifest {
    attributes(
            'Manifest-Version': '1.0',
            'Package': project.group,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': projectOwner,
            'Built-By': projectOwner,
            'Built-Date': new Date().toString(),
            'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
            'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    )
}

def pomConfig = {
    name project.name
    packaging 'jar'
    url projectUrl

    scm {
        url projectScmUrl
        connection projectScmConnection
        developerConnection projectScmDeveloperConnection
    }

    licenses {
        license {
            name mavenLicenseName
            url projectLicenseUrl
        }
    }

    developers {
        developer {
            id projectOwnerId
            name projectOwner
            email projectOwnerEmail
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

jar {
    manifest = project.manifest { from sharedManifest }
    from('.') { include 'LICENSE' into('META-INF') }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    manifest = project.manifest { from sharedManifest }
    from('.') { include 'LICENSE' into('META-INF') }
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    manifest = project.manifest { from sharedManifest }
    from('.') { include 'LICENSE' into('META-INF') }
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.description)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApiKey"

    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'gradle-plugins'
        name = project.name
        websiteUrl = projectUrl
        issueTrackerUrl = projectissueTrackerUrl
        vcsUrl = projectVcsUrl
        licenses = bintrayLicenseName
        labels = ['gradle', 'gradle-plugin', 'semantic-version', 'vcs', 'versioning', 'auto-versioning', 'accuver', 'subversion', 'git', 'mercurial']
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.github.moleksyuk.vcs-semantic-version:com.github.moleksyuk:vcs-semantic-version']
            gpg {
                sign = true
                passphrase = "$gpgPassphrase"
            }
            mavenCentralSync {
                sync = true
                user = "$sonatypeUserToken"
                password = "$sonatypeUserTokenPassword"
                close = '1'
            }
        }
    }
}